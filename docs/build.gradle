buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.asciidoctor:asciidoctor-gradle-jvm:3.3.2"
    }
}

apply plugin: "org.asciidoctor.jvm.convert"

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

asciidoctor {
    resources {
        from('src/docs/images')
        into "./images"
    }

    attributes 'experimental'  : 'true',
            'compat-mode'   : 'true',
            'toc'           : 'left',
            'icons'         : 'font',
            'version'       : project.version,
            'sourcedir'     : "${rootProject.allprojects.find { it.name == 'lib'}.projectDir}/src/main/groovy"
}

configurations {
    documentation
}

dependencies {
    documentation project(path: ':lib')
}

task apidocs(type: Groovydoc, group: 'documentation') {
    def allProjects = rootProject.allprojects
    source allProjects.collect { project ->
        project.files('src/main/groovy')
    }

    destinationDir = new File(buildDir, 'docs/api')
    // Might need a classpath
    docTitle = "Libray ${version}"

    classpath = configurations.documentation
    groovyClasspath = configurations.documentation
}